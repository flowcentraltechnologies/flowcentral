// UPL template for static files generation page.
// Copyright 2021-2024 FlowCentral Technologies Limited.
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you may not
// use this file except in compliance with the License. You may obtain a copy of
// the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// License for the specific language governing permissions and limitations under
// the License.

!ui-page caption:$m{extensionmodulestaticfilesgenerationpage.title} components:$c{basePanel}

// Base panel
!ui-panel:basePanel layout:$d{!ui-vertical style:$s{width:100%;}}
	components:$c{headerBar generationPanel}

// Header
!ui-panel:headerBar layout:$d{!ui-horizontal style:$s{width:100%;} cellType:bottom}
	components:$c{generateLabel} 
	
!ui-label:generateLabel styleClass:$e{fc-largelabel}
	caption:$m{extensionmodulestaticfilesgenerationpage.header.title}

// Generation panel
!ui-panel:generationPanel styleClass:$e{fc-licenseoptions}
	components:$c{generationCaption generationBodyPanel}
!ui-label:generationCaption styleClass:$e{label}
	caption:$m{extensionmodulestaticfilesgenerationpage.generationdetails.caption}
!ui-panel:generationBodyPanel
	layout:$d{!ui-vertical style:$s{width:100%;} showCaption:true cellStyle:$s{padding:5px 2px;}}
	components:$c{frmBasePackage frmRemoteRepoName frmRemoteRepoBranch generateBtn}	
!ui-name:frmBasePackage caption:$m{extensionmodulestaticfilesgeneration.form.basepackage}
	period:true case:lower binding:basePackage required:true size:48

!ui-select:frmRemoteRepoName caption:$m{extensionmodulestaticfilesgeneration.form.remotereponame}
	list:$s{remoterepoconfiglist} blankOption:$s{} binding:remoteRepoName
	eventHandler:$d{!ui-event event:onchange action:$c{repoChangeAct}}
!ui-select:frmRemoteRepoBranch caption:$m{extensionmodulestaticfilesgeneration.form.remoterepobranch}
	list:$s{remoterepobranchlist} listParams:$l{remoteRepoName} blankOption:$s{} binding:remoteRepoBranch

!ui-button:generateBtn styleClass:$e{fc-bluebutton} caption:$m{button.generate}
	eventHandler:$d{!ui-event event:onclick action:$c{generateAct}}
	
// Actions
!ui-post:generateAct path:$n{/generateStaticFiles} components:$c{generationBodyPanel}
!ui-post:repoChangeAct path:$n{/repoChange} components:$c{generationBodyPanel}	