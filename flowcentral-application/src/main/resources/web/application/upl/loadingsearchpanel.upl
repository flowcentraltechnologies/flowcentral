// UPL template for loading search panel
// Copyright 2021-2022 FlowCentral Technologies Limited.
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you may not
// use this file except in compliance with the License. You may obtain a copy of
// the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// License for the specific language governing permissions and limitations under
// the License.

!fc-loadingsearchpanel layout:$d{!ui-vertical}
	components:$c{headerBar searchEntriesPanel colorLegend searchResultPanel footerActionPanel}

// Header Bar
!ui-panel:headerBar styleClass:$s{hdbar} layout:$d{!ui-horizontal style:$s{width:100%;}}
	components:$c{headerLeftPanel}
	
!ui-panel:headerLeftPanel style:$s{display:inline-block; float:left;} layout:$d{!ui-horizontal cellType:middle cellStyle:$s{padding-right:8px;}}
	components:$c{sectorIcon titleBlock}
!fc-sectoricon:sectorIcon binding:sectorIcon
!ui-panel:titleBlock style:$s{display:inline-block;} components:$c{title subTitle}
!ui-label:title styleClass:$s{hdtitle} binding:entitySubTitle
!ui-label:subTitle styleClass:$s{hdsubtitle} binding:entityTitle

// Search entries panel
!ui-panel:searchEntriesPanel layout:$d{!ui-horizontal style:$s{width:100%;} widths:$l{85% none} cellPadding:false}
	components:$c{searchEntries searchEntriesAction} ignoreParentState:true
!fc-search:searchEntries binding:searchEntries
!ui-panel:searchEntriesAction style:$s{padding-left:8px;} layout:$d{!ui-horizontal}
	components:$c{searchBtn clearBtn}
!ui-button:searchBtn caption:$m{button.search} styleClass:$e{fc-bluebutton fc-tinybutton}
	eventHandler:$d{!ui-event event:onclick action:$c{searchAct}}
!ui-button:clearBtn caption:$m{button.clear} styleClass:$e{fc-tinybutton}
    eventHandler:$d{!ui-event event:onclick action:$c{clearAct}}

// Color legend
!ui-colorlegend:colorLegend style:$s{width:100%;} columns:6 binding:loadingTable.colorLegendInfo

// Search result panel
!ui-panel:searchResultPanel layout:$d{!ui-vertical} components:$c{searchResultTbl}
!fc-loadingtable:searchResultTbl binding:loadingTable multiSelect:true valueMarker:$f{searchResultMarker}
	multiSelDependentList:$c{tblActionSel tblActionBtns commitActionBtn} viewButtonClass:$s{mbtn mblue}
	
// Footer action panel
!ui-panel:footerActionPanel style:$s{width:100%;} components:$c{selFooterActionPanel tblActionBtns commitActionBtn}
!ui-panel:selFooterActionPanel layout:$d{!ui-horizontal} components:$c{tblActionSel applyTblActionBtn}
!ui-select:tblActionSel styleClass:$e{footersel} list:$s{tableactionlist} listKey:$s{policy} blankOption:$m{select.apply.toselected}
	listParams:$l{appTableId} binding:appTableActionPolicy
!ui-button:applyTblActionBtn caption:$m{button.applyaction}
    eventHandler:$d{!ui-event event:onclick action:$c{applyTableAct}}
!ui-buttongroup:tblActionBtns style:$s{display:inline-block;} binding:appTableActionButtonInfo
    eventHandler:$d{!ui-event event:onclick action:$c{applyTableBtnAct}}
!ui-button:commitActionBtn styleClass:$e{fc-orangebutton} style:$s{margin-left:20px;} caption:$m{button.commit} captionBinding:$s{commitCaption}
    eventHandler:$d{!ui-event event:onclick action:$c{commitChangeAct}}

// Actions
!ui-postcommand:commitChangeAct command:commitChange
!ui-postcommand:searchAct command:search components:$c{searchEntries}
!ui-postcommand:clearAct command:clear
!ui-postcommand:applyTableAct command:applyTableAction refresh:$c{searchResultPanel} components:$c{searchResultTbl tblActionSel}
!ui-postcommand:applyTableBtnAct command:applyTableBtnAction refresh:$c{searchResultPanel} components:$c{searchResultTbl}
!ui-postcommand:detailsAct command:details refresh:$c{searchResultPanel} components:$c{searchResultTbl}


