// UPL template for new application dialog panel
// Copyright 2021-2024 FlowCentral Technologies Limited.
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you may not
// use this file except in compliance with the License. You may obtain a copy of
// the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// License for the specific language governing permissions and limitations under
// the License.

!fc-newapplicationdialogpanel
    dialogBodyComponents:$c{createBaseBodyPanel}
    dialogActionLayout:$d{!ui-vertical style:$s{width:100%;}}
    dialogActionComponents:$c{createActionPanel}

!ui-panel:createBaseBodyPanel style:$s{width:100%;height:320px;overflow-y: scroll;}
    components:$c{createApplicationBasePanel}

// Create application panel
!ui-panel:createApplicationBasePanel layout:$d{!ui-vertical style:$s{width:100%;}}
	components:$c{formTitle createFormPanel}
!ui-label:formTitle styleClass:$s{createtitle} caption:$m{switchapplicationdialogpanel.form.title}
!ui-formpanel:createFormPanel
	layout:$d{!ui-vertical style:$s{table-layout:fixed;}}
    formCaptionSuffix:$s{:} formBinding:$g{createAppBinding} formColumns:2
	formSection:$d{!ui-section components:$c{frmAppName frmAppDesc frmAppLabel}}
	formSection:$d{!ui-section components:$c{frmCreateModule frmModuleId}}
	formSection:$d{!ui-section components:$c{frmModuleName frmModuleDesc frmModuleLabel frmModuleShortCode}}
!ui-name:frmAppName caption:$m{switchapplicationdialogpanel.form.applicationname}
	size:32 binding:applicationName required:true focus:true
!ui-text:frmAppDesc caption:$m{switchapplicationdialogpanel.form.applicationdesc}
	size:32 binding:applicationDesc required:true
!ui-text:frmAppLabel caption:$m{switchapplicationdialogpanel.form.applicationlabel}
	size:32 binding:applicationLabel required:true
!ui-checkbox:frmCreateModule caption:$m{switchapplicationdialogpanel.form.createmodule}
	binding:createModule
    eventHandler:$d{!ui-event event:onchange action:$c{createModuleChangeAct}}
!ui-select:frmModuleId caption:$m{switchapplicationdialogpanel.form.module}
	list:$s{modulelist} blankOption:$s{} binding:moduleId required:true
!ui-name:frmModuleName caption:$m{switchapplicationdialogpanel.form.modulename}
	size:32 binding:moduleName required:true
!ui-text:frmModuleDesc caption:$m{switchapplicationdialogpanel.form.moduledesc}
	size:32 binding:moduleDesc required:true
!ui-text:frmModuleLabel caption:$m{switchapplicationdialogpanel.form.modulelabel}
	size:32 binding:moduleLabel required:true
!ui-name:frmModuleShortCode caption:$m{switchapplicationdialogpanel.form.moduleshortcode}
	size:8 minLen:3 maxLen:3 binding:moduleShortCode required:true

// Action buttons
!ui-panel:createActionPanel layout:$d{!ui-horizontal} components:$c{createAppBtn cancelCreateBtn}
!ui-button:cancelCreateBtn styleClass:$e{ui-button-alert} caption:$m{button.cancel}
    eventHandler:$d{!ui-event event:onclick action:$c{closeDialogAct}}
!ui-button:createAppBtn styleClass:$e{fc-actbutton} caption:$m{button.createapplication}
    eventHandler:$d{!ui-event event:onclick action:$c{dialogCreateAppAct}}

// Actions
!ui-postcommand:createModuleChangeAct components:$c{createFormPanel}
!ui-post:dialogCreateAppAct components:$c{createFormPanel} validations:$c{formValidation}
	 path:$f{dialogCreateApplicationPath}
	 
// Validations
!ui-validation:formValidation components:$c{createFormPanel}
	