// UPL template for login sequence left aligned
// Copyright (c) 2021-2025 FlowCentral Technologies Limited.
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you may not
// use this file except in compliance with the License. You may obtain a copy of
// the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// License for the specific language governing permissions and limitations under
// the License.

!ui-page caption:$m{security.application.login}
	layout:$d{!ui-horizontal cellStyle:$s{padding:0px;} style:$s{width:100%;height:100%;} widths:$l{392px none} heights:$l{100% 100%}}
	components:$c{loginSection brandSection}

// Brand section
!ui-panel:brandSection style:$s{width:100%;height:100%}
    backImageSrc:$s{web/images/applicationloginbackground.jpg}

// Login section
!ui-panel:loginSection styleClass:$e{fc-loginsection}  style:$s{width:100%;height:100%} components:$c{loginBox}

!ui-panel:loginBox styleClass:$e{fc-loginbox} layout:$d{!ui-vertical style:$s{width:100%;}}
	components:$c{headerSection loginCaptionBox loginSequencePanel}

// Header Section
!ui-panel:headerSection styleClass:$e{fc-loginheader} components:$c{topBannerImg}
!ui-image:topBannerImg src:$s{web/images/applicationloginbanner.png}
	styleClass:$e{fc-loginheaderimg}

// Login caption
!ui-panel:loginCaptionBox styleClass:$e{fc-logincaptionbox} layout:$d{!ui-vertical style:$s{width:100%;}}
	 components:$c{loginTitle loginSubTitle}
!ui-label:loginTitle styleClass:$e{fc-logintitle} caption:$m{common.application.login.title}
	bindingOptional:true binding:loginTitle
!ui-label:loginSubTitle styleClass:$e{fc-loginsubtitle} caption:$m{common.application.login.subtitle}
	bindingOptional:true binding:loginSubtitle

// Login sequence
!ui-switchpanel:loginSequencePanel styleClass:$e{fc-loginsequencebox} layout:$d{!ui-vertical style:$s{width:100%;}}
    components:$c{loginPanel changePasswordPanel validateOTPPanel}

// Login
!ui-panel:loginPanel layout:$d{!ui-vertical style:$s{width:100%;} showCaption:true cellStyle:$s{padding:5px 5px;}}
	components:$c{frmUserName frmUserPassword frmLoginTenantId frmLoginMsg frmLoginBtn}	
!ui-name:frmUserName caption:$m{security.application.loginid}
	styleClass:$e{fc-logintxt} binding:userName case:upper focus:true
    eventHandler:$d{!ui-event event:onenter action:$c{loginAct}}
!ui-password:frmUserPassword caption:$m{security.application.password}
	styleClass:$e{fc-logintxt} binding:password
    eventHandler:$d{!ui-event event:onenter action:$c{loginAct}}
!ui-select:frmLoginTenantId list:logintenantlist styleClass:$e{fc-logintxt} binding:loginTenantId
	caption:$m{security.application.businessunit}
!ui-label:frmLoginMsg styleClass:$e{ui-loginmsg} binding:loginMessage
!ui-button:frmLoginBtn caption:$m{security.application.signin} styleClass:$e{fc-loginbtn}
	eventHandler:$d{!ui-event event:onclick action:$c{loginAct}}

// 2FA
!ui-panel:validateOTPPanel layout:$d{!ui-vertical style:$s{width:100%;} showCaption:true cellStyle:$s{padding:5px 5px;}}
	components:$c{frmOneTimePasscode frmValidateOTPMsg frmValidateOTPBtn cancelValidateOTPBtn}	
!ui-text:frmOneTimePasscode caption:$m{security.application.onetimepassword}
	styleClass:$e{fc-logintxt} binding:oneTimePasscode focus:true required:true
    eventHandler:$d{!ui-event event:onenter action:$c{validateOTPAct}}
!ui-label:frmValidateOTPMsg styleClass:$e{ui-loginmsg} binding:validateOTPMsg
!ui-button:frmValidateOTPBtn caption:$m{security.application.validateotp} styleClass:$e{fc-loginbtn}
	eventHandler:$d{!ui-event event:onclick action:$c{validateOTPAct}}
!ui-button:cancelValidateOTPBtn caption:$m{button.cancel} styleClass:$e{fc-logincancelbtn}
	eventHandler:$d{!ui-event event:onclick action:$c{revertLoginAct}}

// Change password
!ui-panel:changePasswordPanel layout:$d{!ui-vertical style:$s{width:100%;} showCaption:true cellStyle:$s{padding:5px 5px;}}
    components:$c{oldPassword newPassword confirmPassword pwdChangeMsg changePwdBtn cancelPwdBtn}
!ui-password:oldPassword styleClass:$e{fc-logintxt} binding:oldPassword caption:$m{security.application.oldpassword} focus:true
!ui-password:newPassword styleClass:$e{fc-logintxt} binding:newPassword caption:$m{security.application.newpassword}
!ui-password:confirmPassword styleClass:$e{fc-logintxt} binding:confirmPassword caption:$m{security.application.confirmpassword}
    eventHandler:$d{!ui-event event:onenter action:$c{changePwdAct}}
!ui-label:pwdChangeMsg styleClass:$e{ui-loginmsg} binding:chgPwdMessage
	htmlEscape:false
!ui-button:changePwdBtn caption:$m{security.application.changepassword} styleClass:$e{fc-loginbtn}
	eventHandler:$d{!ui-event event:onclick action:$c{changePwdAct}}
!ui-button:cancelPwdBtn caption:$m{button.cancel} styleClass:$e{fc-logincancelbtn}
	eventHandler:$d{!ui-event event:onclick action:$c{revertLoginAct}}

// Select branch popup
!fc-selectbranchpanel:selectBranchPanel dialogCaption:$m{security.application.select.branch}
    branchListBinding:branchInfoList selectRolePath:$n{/selectUserBranch} closeDialogPath:$n{/cancelSelectUserBranch}

// Select user role popup
!fc-selectrolepanel:selectRolePanel dialogCaption:$m{security.application.select.role}
    roleListBinding:userRoleList selectBranchPath:$n{/selectUserRole} closeDialogPath:$n{/cancelSelectUserRole}


// Actions
!ui-post:loginAct path:$n{/login} components:$c{loginPanel}
!ui-post:changePwdAct path:$n{/changeUserPassword} components:$c{oldPassword newPassword confirmPassword}
!ui-post:revertLoginAct path:$n{/revertLogin}
!ui-post:validateOTPAct path:$n{/validateOTP} components:$c{validateOTPPanel}
       